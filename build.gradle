subprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "maven"
    apply plugin: "signing"

    project.group = 'com.fnklabs.draenei'
    project.version = 'undefined'

    if (project.hasProperty("teamcity")) {
        project.version = project.teamcity["build.number"]
    }

    println("App module version:" + project.version)

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "http://dl.bintray.com/akuznetsov/russianmorphology" }
    }

    buildscript {
        repositories {
            mavenLocal()
            mavenCentral()
            maven { url "http://dl.bintray.com/jfrog/jfrog-jars" }
        }
        dependencies {
            classpath(group: "org.jfrog.buildinfo", name: "build-info-extractor-gradle", version: "2.2.4")
        }
    }

    configurations {
        compile.exclude module: "slf4j-log4j12"
        compile.exclude module: "hibernate-validator"
    }

    ext {
        vers = [
                beanutils   : '1.9.2',
                cacheapi    : '1.0.0',
                cassandra   : '3.1.4',
                commonsmath : '2.2',
                commonslang3: '3.5',
                guava       : '18.0',
                jodatime    : '2.9.4',
                ignite      : '1.9.0',
                intellij    : '12.0',
                junit       : '4.12',
                jmh         : '1.11.2',
                logback     : '1.2.1',
                metrics     : '0.1.14',
                mockito     : '2.7.21',
                morphology  : '1.1',
                slf4j       : '1.7.22',
        ]
    }

    task javadocJar(type: Jar) {
        classifier = "javadoc"
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = "sources"
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    signing {
        sign configurations.archives
    }


    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name "draenei"
                    packaging "jar"
                    description "ORM for Cassandra but with distributed cache and analytics utils from package"
                    url "https://github.com/fnklabs/draenei"

                    scm {
                        url "scm:git@github.com:fnklabs/draenei.git"
                        connection "scm:git@github.com:fnklabs/draenei.git"
                        developerConnection "scm:git@github.com:fnklabs/draenei.git"
                    }

                    licenses {
                        license {
                            name "The Apache Software License, Version 2.0"
                            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                            distribution "repo"
                        }
                    }

                    developers {
                        developer {
                            id "aysheka"
                            name "Alisher Alimov"
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.1"
}

